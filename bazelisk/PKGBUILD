# Maintainer: Benjamin Denhartog <ben@sudoforge.com>
# Contributor: Marc Plano-Lesay <marc.planolesay@gmail.com>

pkgname=bazelisk
pkgver=1.9.0
pkgrel=2
pkgdesc='A user-friendly launcher for Bazel.'
arch=('x86_64' 'aarch64')
license=('Apache')
url='https://github.com/bazelbuild/bazelisk'
makedepends=('git' 'go')
conflicts=('bazel')
provides=('bazel')
source=(
  "bazelisk-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
)
sha256sums=('ce5011573df8114b3ac3bd2f21130ace9368abf19ae605d086ce1efdf1033f20')

build() {
  cd "bazelisk-${pkgver}"
  GOPATH=$PWD/go go build
  export BAZELISK_HOME=$PWD/bazelisk_tmp
  case $CARCH in
      x86_64) export target=amd64;;
      aarch64) export target=arm64;;
  esac
  ./bazelisk --output_base=bazel_tmp --max_idle_secs=5 \
      build -c opt //:bazelisk-linux-$target
  ./bazelisk shutdown
}

package() {
  cd "bazelisk-${pkgver}"

  install -Dm644 \
    "LICENSE" \
    "${pkgdir}/usr/share/licenses/bazelisk/LICENSE"

  install -Dm755 \
    bazel-out/*-opt-*/bin/bazelisk-linux_$target \
    "${pkgdir}/usr/bin/${pkgname%isk}"
}

